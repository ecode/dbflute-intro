buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.moowork.node" version "0.9"
    id "com.moowork.gulp" version "0.9"
    id "com.github.hierynomus.license" version "0.11.0"
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'war'
apply plugin: 'project-report'

group = 'org.dbflute'
version = '0.0.3-SNAPSHOT'

ext {
    encoding = 'UTF-8'

    versions =
        [
            jdk : '1.8',
            gradle : '2.3',

            slf4j : '1.7.12',
            logback : '1.1.3',

            lastaflute : '0.6.0-SNAPSHOT',
            dbflute : '1.1.0-sp3-00-SNAPSHOT',

            junit : '4.12',
            utflute : '0.6.0-SNAPSHOT'
        ]
}

fileTree(dir: 'gradle/gradlePlugin', includes: ['*.gradle']).each { apply from: it }

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'commons-io:commons-io:2.4'

    compile "org.slf4j:slf4j-api:${versions.slf4j}"
    runtime "ch.qos.logback:logback-classic:${versions.logback}"

    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    runtime 'javax.el:javax.el-api:3.0.0'
    compile 'javax.transaction:javax.transaction-api:1.2'

    compile "org.dbflute:dbflute-runtime:${versions.dbflute}"
    compile "org.dbflute.lasta:lastaflute:${versions.lastaflute}"

    providedCompile 'org.eclipse.jetty:jetty-webapp:9.2.10.v20150310'
    providedCompile 'org.eclipse.jetty:jetty-annotations:9.2.10.v20150310'
}

mainClassName = 'org.dbflute.boot.DbfluteIntroBoot'

war {
    from {
        configurations.providedCompile.collect {
            it.isDirectory() ? it : project.zipTree(it)
        }
    }

    from fileTree(dir: sourceSets.main.output.classesDir, include: '**/org/dbflute/boot/*Boot.class')
    from fileTree(dir: file('static/dist'), include: '**/**')

    includeEmptyDirs = false

    version = ''

    manifest {
        attributes 'Implementation-Title': project.name
        attributes 'Implementation-Version': project.version
        attributes 'SCM-Branch': 'TODO'
        attributes 'SCM-Revision': 'TODO'
        attributes 'Build-Timestamp': 'TODO'
        attributes 'Jenkins-Build-Number': 'TODO'
        attributes 'Main-Class' : mainClassName
    }
}

license {
    strictCheck true
    include "**/*.java"
    mapping {
        java='SLASHSTAR_STYLE'
    }
}
